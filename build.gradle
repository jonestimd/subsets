apply plugin: 'java'
apply plugin: 'maven'
apply plugin: 'signing'

compileJava.sourceCompatibility = 1.8

group = 'io.github.jonestimd'
version = '1.1-SNAPSHOT'

repositories {
    mavenCentral()
}

dependencies {
    compile( group: 'com.google.guava', name: 'guava', version: '18.0' )

    testCompile( group: 'junit', name: 'junit', version: '4.7' )
    testCompile( group: 'org.easytesting', name: 'fest-assert', version: '1.4' )
    testCompile( group: 'org.easytesting', name: 'fest-util', version: '1.1.6' )
    testCompile( group: 'org.mockito', name: 'mockito-core', version: '1.9.0' )
}

jar {
    manifest {
        attributes 'Implementation-Title': 'io.github.jonestimd:subsets',
                   'Implementation-Version': version
    }
}

javadoc {
    options.addStringOption('Xdoclint:none', '-quiet')
}

task javadocJar(type: Jar) {
    classifier = 'javadoc'
    from javadoc
}

task sourceJar(type: Jar) {
    classifier "sources"
    from(sourceSets.main.allSource)
}

artifacts {
    archives javadocJar, sourceJar
}

if (hasProperty('signing.keyId')) signing {
    sign configurations.archives
}

uploadArchives {
    repositories {
        mavenDeployer {
            beforeDeployment { MavenDeployment deployment -> signing.signPom(deployment) }

            if (hasProperty('ossrhUsername')) repository(url: "https://oss.sonatype.org/service/local/staging/deploy/maven2/") {
                authentication(userName: ossrhUsername, password: ossrhPassword)
            }

            snapshotRepository(url: 'file:///' + System.properties['user.home'] + '/.m2/repository')

            pom.project {
                name = rootProject.group + ':' + rootProject.name
                packaging = 'jar'
                description = 'Utilities for finding subsets of a collection that meet some criteria'
                url = 'https://jonestimd.github.io/subsets/'

                scm {
                    connection = 'scm:git:https://github.com/jonestimd/subsets.git'
                    developerConnection = 'scm:git:https://github.com/jonestimd/subsets.git'
                    url = 'https://github.com/jonestimd/subsets'
                }

                licenses {
                    license {
                        name = 'The Unlicense'
                        url = 'http://unlicense.org/UNLICENSE'
                    }
                }

                developers {
                    developer {
                        name = 'Tim Jones'
                        // organization = 'Github'
                        organizationUrl = 'https://github.com/jonestimd'
                        email = 'jonestimd@users.noreply.github.com'
                    }
                }
            }
        }
    }
}